#version 450

layout (constant_id = 0) const uint INPUT_SIZE = 3;
layout (constant_id = 1) const uint INPUT_DEPTH = 1;
layout (constant_id = 2) const uint KERNEL_SIZE = 3;
layout (constant_id = 3) const uint KERNEL_OFFSET = 3;
layout (constant_id = 4) const uint OUTPUT_SIZE = 3;
layout (constant_id = 5) const uint OUTPUT_DEPTH = 1;
layout (constant_id = 6) const uint WORKGROUP_SIZE = 1;
layout (constant_id = 7) const uint STRIDE = 1;

layout(binding = 0) buffer Inp {
  float input_data[ ];
};

layout(binding = 1) buffer k {
  float kernel[ ];
};

layout(binding = 2) buffer Bias {
  float bias_data[ ];
};

layout(binding = 3) buffer Outp {
  float output_data[ ];
};

//https://www.khronos.org/registry/OpenGL/extensions/ARB/ARB_gl_spirv.txt
// in gl_NumWorkGroups
// in gl_WorkGroupSize
// in gl_WorkGroupID
// in gl_LocalInvocationID
// in gl_GlobalInvocationID
// in gl_LocalInvocationIndex

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout (local_size_x_id = 6) in;
layout (local_size_y_id = 6) in;
layout (local_size_z_id = 5) in;

void main() {
  // return when out of bounds (i.e. the workgroup size does not fit the output size)
  if (gl_GlobalInvocationID.x >= OUTPUT_SIZE || gl_GlobalInvocationID.y >= OUTPUT_SIZE)
    return;

  float sum = bias_data[gl_GlobalInvocationID.z];
  uint kernel_offset = gl_GlobalInvocationID.z*KERNEL_OFFSET;
  uint input_x_base = gl_GlobalInvocationID.x*STRIDE;
  uint input_x_offset = 0;
  uint input_y_base = gl_GlobalInvocationID.y*STRIDE;
  uint input_y_offset = 0;
  for (int y=0; y<KERNEL_SIZE; ++y) {
    input_y_offset = input_y_base + y;
    for (int x=0; x<KERNEL_SIZE; ++x) {
      input_x_offset = input_x_base + x;
      for (int z=0; z<INPUT_DEPTH; ++z) {
        sum += kernel[kernel_offset+(y*KERNEL_SIZE + x)*INPUT_DEPTH  + z]
          * input_data[(input_y_offset* INPUT_SIZE + input_x_offset)*INPUT_DEPTH + z];
      }
    }
  }
  output_data[(gl_GlobalInvocationID.y*OUTPUT_SIZE + gl_GlobalInvocationID.x)*OUTPUT_DEPTH + gl_GlobalInvocationID.z] = sum < 0 ? 0 : sum > 6 ? 6 : sum;

}
